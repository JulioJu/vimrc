# Heavy inspired from https://aur.archlinux.org/cgit/aur.git/tree/PKGBUILD?h=rxvt-unicode-patched
# Author Julio Prayer

_pkgname=rxvt-unicode
pkgname=rxvt-unicode-patched
pkgver=9.22
pkgrel=10
pkgdesc='Unicode enabled rxvt-clone terminal emulator (urxvt) with fixed font spacing'
arch=('i686' 'x86_64')
url='http://software.schmorp.de/pkg/rxvt-unicode.html'
license=('GPL')
depends=('libxft' 'perl' 'startup-notification' 'rxvt-unicode-terminfo')
optdepends=('gtk2-perl: to use the urxvt-tabbed')
provides=(rxvt-unicode)
conflicts=(rxvt-unicode)
source=(http://dist.schmorp.de/rxvt-unicode/$_pkgname-$pkgver.tar.bz2
        'urxvt.desktop'
        'urxvtc.desktop'
        # 'urxvt-tabbed.desktop'
        'font-width-fix.patch'
        'line-spacing-fix.patch'
        'https://gist.githubusercontent.com/alexoj/df5bae7a4825cb596581/raw/75a1e75c2ae1ec5c0db68a29f8a6821e9e3d87a5/sgr-mouse-mode.patch'
        'fix-smart-resize-with-x11-frame-borders.patch'  # will be in 9.22+
        )
sha1sums=('e575b869782fbfed955f84f48b204ec888d91ba1'
          'b5a4507f85ebb7bac589db2e07d9bc40106720d9'
          '62c4ffecfce6967def394dd4d418b68652372ea2'
          '01ee8f212add79a158dcd4ed78d0ea1324bdc59b'
          'b7fde1c46af45e831828738874f14b092b1e795f'
          'dfbc8729c545105eff21e20ef3a4a3841a68a192'
          '6dfa49a211c48193c8d87fb9993ed459b2b4387b')

prepare() {
  cd $_pkgname-$pkgver

  patch -p0 -i ../font-width-fix.patch
  patch -p0 -i ../line-spacing-fix.patch
  # patch -p0 -i ../sgr-mouse-mode.patch
  # patch -p1 -i ../fix-smart-resize-with-x11-frame-borders.patch
}

build() {
  cd $_pkgname-$pkgver

  # do not specify --with-terminfo (FS#46424)
  ./configure \
    --prefix=/usr \
    --enable-256-color \
    --enable-startup-notification \
    --enable-frills \
    --enable-pixbuf \
    --disable-transparency \
    --disable-afterimage \
    --disable-smart-resize \
    --enable-combining \
    --enable-iso14755 \
    --enable-unicode3 \
    --enable-font-styles \
    --enable-fading \
    --enable-xft \
    --enable-xim \
    --disable-perl \
    --disable-mousewheel \
    --disable-keepscrolling \
    --disable-slipwheeling \
    --disable-next-scroll \
    --disable-rxvt-scroll \
    --disable-selectionscrolling \
    --disable-xterm-scroll \
    --disable-plain-scroll \
    --disable-pointer-blank \
    --enable-lastlog \
    --enable-utmp \
    --enable-wtmp
  make
}

package() {

  # install freedesktop menu
  for _f in urxvt urxvtc; do
    install -Dm644 $_f.desktop "$pkgdir/usr/share/applications/$_f.desktop"
  done

  cd $_pkgname-$pkgver

  # workaround terminfo installation
  export TERMINFO="$srcdir/terminfo"
  install -d "$TERMINFO"
  make DESTDIR="$pkgdir" install

  # # install the tabbing wrapper ( requires gtk2-perl! )
  # sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  # install -Dm 755 doc/rxvt-tabbed "$pkgdir/usr/bin/urxvt-tabbed"
}

